# Lucas's Zsh config 
# Version 25.11.r3

PATH="$HOME/.local/bin:$PATH"

if [[ -z "${SSH_CONNECTION}" ]] && [ $(uname -s) = "Linux" ]; then
    export SSH_AUTH_SOCK="$XDG_RUNTIME_DIR/ssh-agent.socket"
fi

#########################
### Plugin Management ###
#########################
#--Install Zinit if it isn't already there
ZINIT_HOME="${XDG_DATA_HOME:-${HOME}/.local/share}/zinit/zinit.git"
[ ! -d $ZINIT_HOME ] && mkdir -p "$(dirname $ZINIT_HOME)"
[ ! -d $ZINIT_HOME/.git ] && git clone https://github.com/zdharma-continuum/zinit.git "$ZINIT_HOME"
source "${ZINIT_HOME}/zinit.zsh"

#--Plugins - Syntax Highlight, Completions, and Autosuggestions loaded with Zinit turbo mode
zinit wait lucid for \
 atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
    zdharma-continuum/fast-syntax-highlighting \
 blockf \
    zsh-users/zsh-completions \
 atload"!_zsh_autosuggest_start;" \
    zsh-users/zsh-autosuggestions

autoload -U compinit && compinit 
autoload -U bashcompinit && bashcompinit
zstyle ':completion::complete:*' use-cache 1

#############################
### Aliases and Functions ###
#############################

# Shorthand for a couple things
alias ff='clear && fastfetch'
# Colors - from Debian 12 default .bashrc
alias ls='ls --color=auto'
alias dir='dir --color=auto'
alias vdir='vdir --color=auto'

alias grep='grep --color=auto'
alias fgrep='fgrep --color=auto'
alias egrep='egrep --color=auto'
# Extra LS command shorthand
alias lah='ls -lAh'
alias la='ls -lA'
# Easy cd by typing periods .
alias ..='cd ../'
alias ...='cd ../../'
alias ....='cd ../../../'

# Extra verbosity and interactivity in common file operation commands
rm() { command rm -vi "${@}"; }        # Interactive and verbose output for rm, cp, mv
cp() { command cp -vi "${@}"; }        # Prevents file overwrites and explains what is happening
mv() { command mv -vi "${@}"; }        # EX: mv file file2 outputs "renamed file -> file2"

_update_stuffs() {
# check for sudo
if ! command -v sudo >/dev/null 2>&1; then
   echo "Install sudo you dingus"
fi

# update Arch packages
if command -v yay >/dev/null 2>&1; then
   sudo pacman -Syu && yay -Sua
fi

# update Gentoo packages
if command -v emerge >/dev/null 2>&1; then
   sudo emerge --ask --verbose --update --deep --newuse @world
fi

# update Homebrew packages
if command -v brew >/dev/null 2>&1; then
   brew update
   brew outdated
   brew upgrade
   brew cleanup
fi
}

alias up=_update_stuffs

#####################
### Shell Options ###
#####################
setopt correct_all 
export HISTSIZE=5000
export SAVEHIST=$HISTSIZE
export HISTFILE=~/.zsh_history
setopt hist_append
setopt share_history
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_save_no_dups
setopt hist_ignore_space
setopt hist_find_no_dups

# Completion styling by dreamsofautonomy https://github.com/dreamsofautonomy/zensh/blob/main/.zshrc
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"

# colored GCC warnings and errors
export GCC_COLORS='error=01;31:warning=01;35:note=01;36:caret=01;32:locus=01:quote=01'

####################
### Shell Prompt ###
####################
# A zsh prompt loosely based off Gentoo prompt with a few mods:
# - Multicolor!
# - VCS info 
# - Tweaked path and select prompts
# zsh prompt vcs tutorial @: https://salferrarello.com/zsh-git-status-prompt/
lk_prompt_setup () {
prompt_gentoo_prompt=${1:-'blue'}
user_color=${2:-'132'}
divider_color=${3:-'222'}
hostname_color=${4:-'192'}
prompt_gentoo_root=${5:-'red'}

if [ "$USER" = 'root' ]
then
base_prompt="%B%F{$prompt_gentoo_root}%m%k "
else
# %B -> Bold %F -> color %n -> username %m -> short hostname
base_prompt="%B%F{$user_color}%n%F{$divider_color}@%F{$hostname_color}%m%k "
fi
post_prompt="%b%f%k"

# show VCS info in prompt
autoload -Uz add-zsh-hook vcs_info
setopt prompt_subst
add-zsh-hook precmd vcs_info
vcs_prompt='%F{red}${vcs_info_msg_0_}%f'

zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' unstagedstr ' *'
zstyle ':vcs_info:*' stagedstr ' +'
# Set the format of the Git information for vcs_info
zstyle ':vcs_info:git:*' formats       ' (%b%u%c)'
zstyle ':vcs_info:git:*' actionformats ' (%b|%a%u%c)'

path_prompt="%B%F{$prompt_gentoo_prompt}%1~"
PS1="$base_prompt$path_prompt$vcs_prompt %# $post_prompt"
PS2="$path_prompt %_> $post_prompt"
PS3="$path_prompt ?# $post_prompt"
}

lk_prompt_setup "$@"

# Enable vi mode for the shell
bindkey -v
